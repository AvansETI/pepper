topic: ~main ^resetOnFocus()
#### DIT DOCUMENT BEVAT ALLE DIALOGEN VOOR HET ONTMOETEN ####
concept:(robot) [pepper care robot wereld]
concept:(greeting) [hallo hi hey goedemorgen]
concept:(goingGreat) ["Wat fijn om te horen!" "Fantastisch!"]
concept:(time) [tijd hoelaat "Hoelaat is het nu?" "Wat is de tijd?" "Hoelaat is het?"]
concept:(day) [dag "Welke dag is het?" "Wat is de dag vandaag?" "Welke dag?"]
concept:(good) [goed prima "Het gaat goed" "Ik voel me prima"]
concept:(bad) [slecht moe "Het gaat slecht" "Ik voel me slecht"]
concept:(yes) [ja jazeker]
concept:(no) [nee niet]
concept:(silence) [""]
concept:(end) ["Einde interactie"]

concept:(repeat) [herhaal "Kan je het herhalen?"]
concept:(robotRepeat) ["Ik zal het voor je herhalen dot"]

concept:(notUnderstood) ["Ik begrijp u niet helemaal dot" "Ik kan u niet goed verstaan dot"]

#### Begroeting ####
u:(~greeting ~robot) ~greeting ^enableThenGoto(convo)

#### Interactie ####
proposal: %convo Hoe gaat het?
  u1:(~good ^exact) ~goingGreat ^enableThenGoto(ending)
  u1:(~bad ^exact) ^gotoRandom(badfeeling)
  u1:(~repeat) ~robotRepeat ^sameProposal

proposal: %badfeeling Wil je een mop horen? $feelingEvent=mop
  u1: (~no) ^enableThenGoto(ending)
  u1: (~yes) ~jokes ^enableThenGoto(feelingbetter)
  u1: (~repeat) ~robotRepeat ^sameProposal

proposal: %badfeeling Wil je een quote horen? $feelingEvent=quote
  u1: (~no) ^enableThenGoto(ending)
  u1: (~yes) ~quotes ^enableThenGoto(feelingbetter)
  u1: (~repeat) ~robotRepeat ^sameProposal

proposal: %badfeeling Wil je een raadsel horen? $feelingEvent=raadsel
  u1: (~no) ^enableThenGoto(ending)
  u1: (~yes) ~riddles ^enableThenGoto(feelingbetter)
  u1: (~repeat) ~robotRepeat ^sameProposal

proposal: %feelingbetter Wil je nog een $feelingEvent horen?
  u1: (~no) ^enableThenGoto(ending)
  u1: (~yes) ^first[
                "$feelingEvent==mop ~jokes ^sameProposal"
                "$feelingEvent==quote ~quotes ^sameProposal"
                "$feelingEvent==raadsel ~riddles ^sameProposal"
            ]
  u1: (~repeat) ~robotRepeat ^sameProposal

proposal: %ending Nog een fijne dag toegewenst!

#### Extra ####
u:(~time) Het is nu ^currentTime
u:(~day) Vandaag is het ^currentDate

#### Events ####
u:(e:Dialog/NotUnderstood) ~notUnderstood

#### Moppen ####
concept:(jokes) [
    "Hoe noem je het kind van een slechte jager? Een vegetariÃ«r dot"
    "Wat zit er tussen de hemel en aarde? en dot"
    "Wat is het voordeel van dement zijn? Je kan je eigen paaseieren verstoppen dot"
    "Hoe komt het dat de kerstman nooit vooruit gaat? Hij roept altijd ho ho ho dot"
    "Waar koopt een koe zijn kleren? In de boe tiek dot"
]

#### Quotes ####
concept:(quotes) [
    "Om nieuwe dingen in je leven toe te laten zul je soms eerst oude dingen los moeten laten dot"
    "Als je denkt dat alles tegenzit denk dan opnieuw dot"
    "Blijf niet hangen in het verleden dot Dat is voorbij dot Raak niet gestrest van de toekomst dot Zo ver is het nog niet dot Leef in het heden en geniet dot"
    "Wacht niet op een goede dag maak er een dot"
    "Wees dankbaar voor wat je hebt dot Je wil niet weten hoeveel mensen er zijn die dat willen hebben wat jij hebt dot"
]

#### Raadsels ####
concept:(riddles) [
    "Waarom loopt een schaap nogal wankelig?" #Omdat hij lam is geweest
    "Wat is ver te zoeken op een verening voor doven en slechthorenden?" #Saamhorigheid
    "Wat is het meervoud van auto?" #file
    "Hoe noem je een raadselachtige kerst?" #Een Quizmas
    "Wat staat er op het graf van een robot?" #Roest in vrede
]