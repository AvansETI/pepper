topic: ~dialog ^resetOnFocus()
#### DIT DOCUMENT BEVAT DE DIALOGEN VOOR DE SCHERMEN ####
concept:(days) [1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 27 28 29 30 31]
concept:(months) [1 2 3 4 5 6 7 8 9 10 11 12]
concept:(start) [start "Ik wil graag een gesprek starten"]
concept:(scale) [1 2 3 4 5 6 7 8 9 10]
concept:(options) [opties "Welke maaltijden zijn er"]
concept:(order) [bestel order een "Doe mij maar" "Ik wil graag" "Ik kies" "Ik wil"]
concept:(favorite) [favo favoriet "Wat is jouw favoriet"]
concept:(myfav) ["Mijn favoriet is" "Ik heb eigenlijk geen favoriet maar als ik echt zou moeten kiezen was dat"]
concept:(cya) ["Ik wens je nog een fijne dag tegemoet!"]

#### LET OP - Bij het toevoegen van nieuwe dynamics zorg ervoor dat alles in lowercase staat ####

#### Intialiseer d.m.v. code ####
dynamic: name
dynamic: meals
dynamic: fav
dynamic: reminders
dynamic: questions

#### Intialiseer d.m.v. topic ####
dynamic: meal
dynamic: answered
dynamic: number
dynamic: explain

#### Dialoog ####
u:(~robot ~start ^exact) ^execute(navigateScreen, nav, INTRO) ^enableThenGoto(INTRO)

proposal: %INTRO Welkom mijn naam is Pepper! Vandaag zal ik het werk van mijn collega overnemen ^execute(navigateScreen, nav, ACCESS) ^enableThenGoto(ACCESS)

proposal: %ACCESS Bij het gebruik van pepper wordt er informatie met de verpleegkundige gedeeld, ga je hiermee akkoord ?
    u1: (~no) ^execute(navigateScreen, nav, DENIED) ^enableThenGoto(DENIED)
    u1: (~yes) ^execute(navigateScreen, nav, IDBDAY) ^enableThenGoto(IDBDAY)
    u1: (~repeat) ^sameProposal

proposal: %IDBDAY Om verder te gaan heb ik je geboortedatum nodig
    u1: (_~days _~months _*) ^execute(selectPatientBirthday, pbday, ^concatenate($1, $2, $3)) ^execute(navigateScreen, nav, IDNAME) ^enableThenGoto(IDNAME)
    u1: (~repeat) ^sameProposal

proposal: %IDNAME En wat is je naam?
    u1: (_*) ^execute(selectPatientName, pname, $1) ^enableThenGoto(IDCHECK)
    u1: (~repeat) ^sameProposal

proposal: %IDCHECK Ben jij deze persoon ?
    u1: (~no)  ^execute(confirmDialogSelect, cfdlg, NO) ^enableThenGoto(IDBDAY)
    u1: (~yes)  ^execute(confirmDialogSelect, cfdlg, YES) ^execute(navigateScreen, nav, PATIENT) ^enableThenGoto(PATIENT)
    u1: (~repeat) ^sameProposal

proposal: %PATIENT Wat fijn om je weer te zien {~name} ! ^execute(navigateScreen, nav, ORDER) ^enableThenGoto(ORDER)

proposal: %ORDER Vandaag staan de volgende maaltijden op het menu {^enumerate(~meals)} ^enableThenGoto(ORDERASK)

#### LET OP - Voeg AddToConcept eerst toe anders crashed de chatbot ####
proposal: %ORDERASK Wat wil je bestellen {~name} ?
    u1:(~options) ^enumerate(~meals) ^enableThenGoto(ORDERFALLBACK)
    u1:(~favorite) ~myfav ~fav ^enableThenGoto(ORDERFALLBACK)
    u1:(~order _~meals) ^addToConcept(~meal, $1) ^execute(selectMealItem, sel, $1) ^enableThenGoto(ORDERCHECK)
    u1:(~repeat) ^enableThenGoto(ORDER)

proposal: %ORDERFALLBACK ^empty #Fallback is nodig om de applicatie niet te laten crashen of vast te laten lopen
    u1:(~options) ^enumerate(~meals) ^enableThenGoto(ORDERFALLBACK)
    u1:(~favorite) ~myfav ~fav ^enableThenGoto(ORDERFALLBACK)
    u1:(~order _~meals) ^addToConcept(~meal, $1) ^execute(selectMealItem, sel, $1) ^enableThenGoto(ORDERCHECK)

proposal: %ORDERCHECK Klopt het dat je hebt gekozen voor ~meal ?
    u1: (~no) ^execute(confirmDialogSelect, cfdlg, NO) ^enableThenGoto(ORDER)
    u1: (~yes) ^execute(confirmDialogSelect, cfdlg, YES) ^execute(navigateScreen, nav, REMINDER) ^enableThenGoto(REMINDER)
    u1: (~repeat) ^sameProposal

proposal: %REMINDER {~name} Dit zijn de herinneringen voor vandaag {^enumerate(~reminders)} ! ^execute(navigateScreen, nav, QUESTION) ^enableThenGoto(QUESTION)

proposal: %QUESTION {~name} De verpleegkundige heeft de volgende vraag gesteld {~questions} ! ^enableThenGoto(QUESTIONEXPLAIN)

proposal: %QUESTIONEXPLAIN Wat is jouw antwoord hierop?
    u1: (_*) ^addToConcept(~answered, $1) ^execute(inputFeedbackExplain, qexp, $1) ^enableThenGoto(QUESTIONCHECK)
    u1: (~repeat) ^enableThenGoto(QUESTION)

proposal: %QUESTIONCHECK Je gaf als antwoord {~answered} klopt dit?
    u1: (~no) ^execute(confirmDialogSelect, cfdlg, NO) ^enableThenGoto(QUESTION)
    u1: (~yes) ^execute(confirmDialogSelect, cfdlg, YES) ^execute(navigateScreen, nav, FEEDBACK) ^enableThenGoto(FEEDBACK)
    u1: (~repeat) ^sameProposal

proposal: %FEEDBACK {~name} Hoe voel je je momenteel op een schaal van 1 tot 10?
    u1: (_~scale) ^addToConcept(~number, $1) ^execute(selectFeedbackNumber, fnum, $1) ^enableThenGoto(FEEDBACKEXPLAIN)
    u1: (~repeat) ^sameProposal

proposal: %FEEDBACKEXPLAIN Waarom heb je voor dit cijfer {~number} gekozen?
    u1: (_*) ^addToConcept(~explain, $1) ^execute(inputFeedbackExplain, fexp, $1) ^enableThenGoto(FEEDBACKCHECK)
    u1: (~repeat) ^sameProposal

proposal: %FEEDBACKCHECK Je gaf als antwoord {~explain} klopt dit?
    u1: (~no) ^execute(confirmDialogSelect, cfdlg, NO) ^enableThenGoto(FEEDBACK)
    u1: (~yes) ^execute(confirmDialogSelect, cfdlg, YES) ^execute(navigateScreen, nav, GOODBYE) ^enableThenGoto(GOODBYE)
    u1: (~repeat) ^sameProposal

proposal: %DENIED Helaas kan ik je niet verder helpen Tot ziens! ^execute(navigateScreen, nav, STANDBY)

proposal: %GOODBYE Tot ziens {~name} ~cya ^execute(navigateScreen, nav, STANDBY)