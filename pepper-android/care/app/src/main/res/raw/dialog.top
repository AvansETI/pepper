topic: ~dialog ^resetOnFocus()
#### DIT DOCUMENT BEVAT ALLE DIALOGEN VAN SCHERMEN ZONDER EXTERNE FUNCTIONALITEIT ####
concept:(numbers) [1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16 17 18 19 20 21 22 23 24 25 26 27 28 29 30 31 32 33 34 35 36 37 38 39 40 41 42 43 44 45 46 47 48 49 50 51 52 53 54 55 56 57 58 59 60 61 62 63 64 65 66 67 68 69 70 71 72 73 74 75 76 77 78 79 80 81 82 83 84 85 86 87 88 89 90 91 92 93 94 95 96 97 98 99 100]
concept:(next) [verder volgende next "ga door"]
concept:(options) [opties "Welke maaltijden zijn er"]
concept:(order) [bestel order een "Doe mij maar" "Ik wil graag" "Ik kies"]
concept:(favorite) [favo favoriet "Wat is jouw favoriet"]
concept:(scale) [1 2 3 4 5 6 7 8 9 10]
concept:(start) [start "Ik wil graag een gesprek starten"]

dynamic: name
dynamic: meals

#### Dialoog ####
u:(~start ^exact) ^execute(navigateScreen, nav, INTRO) ^nextProposal

proposal: Welkom mijn naam is Pepper! Vandaag zal ik het werk van mijn collega overnemen dot
    u1: (~next ^exact) ^execute(navigateScreen, nav, ID) ^nextProposal
    u1: (~repeat) ~robotRepeat ^sameProposal

proposal: Om verder te gaan met het proces heb ik uw ID nodig dot
    u1: (~numbers) ^execute(confirmationDialog, dlg, $1) ^nextProposal
    u1: (~repeat) ~robotRepeat ^sameProposal

proposal: Klopt dit?
    u1: (~no) ^previousProposal
    u1: (~yes) ^execute(navigateScreen, nav, PATIENT) ^nextProposal
    u1: (~repeat) ~robotRepeat ^sameProposal

proposal: Wat fijn om u weer te zien ~name!
    u1: (~next ^exact) ^execute(navigateScreen, nav, FEEDBACK) ^nextProposal
    u1: (~repeat) ~robotRepeat ^sameProposal

proposal: Hoe voelt u zich momenteel op een schaal van 1 tot 10?
    u1: (ik voel me een ~scale ^exact) ^nextProposal
    u1: (~repeat) ~robotRepeat ^sameProposal

proposal: Klopt dit?
    u1: (~no) ^previousProposal
    u1: (~yes) ^execute(navigateScreen, nav, GOODBYE) ^nextProposal
    u1: (~repeat) ~robotRepeat ^sameProposal

proposal: Tot ziens ~name dot Ik wens u nog een fijne dag tegemoet!
    u1: (~next ^exact) ^execute(navigateScreen, nav, STANDBY)
    u1: (~repeat) ~robotRepeat ^sameProposal